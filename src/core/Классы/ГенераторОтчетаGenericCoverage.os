
Процедура Сформировать(Знач ДанныеСтатистикиПокрытия, ПутьКВыходномуФайлу) Экспорт

	ДанныеПокрытия = ДанныеСтатистикиПокрытия;

	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ПутьКВыходномуФайлу);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("coverage");
	ЗаписьXML.ЗаписатьАтрибут("version", "1");

	Для Каждого Файл Из ДанныеПокрытия Цикл
		
		ДанныеФайла = Файл.Значение;

		ПутьКФайлу = ДанныеФайла.Получить("#path");

		ЗаписьXML.ЗаписатьНачалоЭлемента("file");
		ЗаписьXML.ЗаписатьАтрибут("path", ПутьКФайлу);
		
		Для Каждого КлючИЗначение Из ДанныеФайла Цикл
			
			Если КлючИЗначение.Ключ = "#path" Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеПроцедуры = КлючИЗначение.Значение;
			Для Каждого ДанныеСтроки Из ДанныеПроцедуры Цикл
				
				ЗаписьXML.ЗаписатьНачалоЭлемента("lineToCover");
				
				ЗаписьXML.ЗаписатьАтрибут("lineNumber", ДанныеСтроки.Ключ);
				Покрыто = Число(ДанныеСтроки.Значение.Получить("count")) > 0;
				ЗаписьXML.ЗаписатьАтрибут("covered", Формат(Покрыто, "БИ=true; БЛ=false"));
				
				ЗаписьXML.ЗаписатьКонецЭлемента(); // lineToCover
			КонецЦикла;
		КонецЦикла;
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); // file
		
	КонецЦикла;

	ЗаписьXML.ЗаписатьКонецЭлемента(); // coverage
	ЗаписьXML.Закрыть();
	
КонецПроцедуры